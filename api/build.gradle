plugins {
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.springframework.boot' version '3.5.4'
  id 'jacoco'
	id 'java'
}

group = 'com.grin'
version = System.getenv("API_VERSION")?:'v1.0.1'

def environment = System.getenv("ENVIRONMENT")?:'dev'
def caching = System.getenv("CACHE_ENVIRONMENT")?:'caffeine'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}


dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Lombok
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'

	// Mapper
	implementation 'org.modelmapper:modelmapper:3.2.0'

	// Database
	runtimeOnly 'org.postgresql:postgresql'


	// Documentation - Swagger
  if(!environment.equalsIgnoreCase("PRODUCTION"))
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

	// Caching
  implementation 'org.springframework.boot:spring-boot-starter-cache:3.1.5'
	switch (caching) {
		case "redis":
			implementation 'org.springframework.boot:spring-boot-starter-data-redis'
			break
		case "caffeine":
		default:
			implementation 'com.github.ben-manes.caffeine:caffeine'
	}

}

tasks.named('test') {
	useJUnitPlatform()
}

jar {
  enabled = false
}
